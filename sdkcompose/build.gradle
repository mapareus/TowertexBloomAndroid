plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    id 'maven-publish'
}

android {
    namespace 'com.towertex.sdkcompose'
    compileSdk compile_sdk_version

    defaultConfig {
        minSdk min_sdk_version
        targetSdk target_sdk_version

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        compose true
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publishing {
    publications {
        aar(MavenPublication) {
            groupId = 'com.towertex.bloom'
            artifactId = 'sdkcompose'
            version = '0.0.2'

            afterEvaluate {
                from components.release
            }
        }
    }

    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/mapareus/TowertexBloomAndroid")
            credentials {
                username = properties.getProperty('gpr.user') ?: System.getenv("USERNAME")
                password = properties.getProperty('gpr.key') ?: System.getenv("TOKEN")
            }
        }
    }
}

dependencies {

    api project(path: ':sdk')

    //glide
    implementation libs.glide

    //compose
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
}